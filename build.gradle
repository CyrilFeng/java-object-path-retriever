buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'java'
apply plugin: 'maven-publish'

// In this section you declare where to find the dependencies of your project
repositories {
    jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "johncannon87"
            name "John Cannon"
            email "john.work.cannon@gmail.com"
        }
    }

    scm {
        url "https://github.com/JohnCannon87/java-object-path-retriever"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.github.johncannon87'
            artifactId 'java-object-path-retriever'
            version '1.0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Small Library to support accessing java variables (via get methods) via dot notation.')
                root.appendNode('name', 'java-object-path-retriever')
                root.appendNode('url', 'https://github.com/JohnCannon87/java-object-path-retriever')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
	user = System.getProperty('bintray.user')
	key = System.getProperty('bintray.key')
	publications = ['mavenPublication']

	pkg {
		repo = 'java-object-path-retriever'
		name = 'java-object-path-retriever'
		userOrg = 'johncannon87'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/JohnCannon87/java-object-path-retriever'
		version {
			name = '1.0.1'
			released  = new Date()
		}
	}

}

dependencies {
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-collections4
	implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'
	// https://mvnrepository.com/artifact/org.projectlombok/lombok
	implementation group: 'org.projectlombok', name: 'lombok', version: '1.18.2'
	// https://mvnrepository.com/artifact/org.apache.commons/commons-text
	implementation group: 'org.apache.commons', name: 'commons-text', version: '1.5'
	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
	
    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
}

